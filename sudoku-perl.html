#!/usr/bin/perl
use strict;
print "content-type: text/html\n\n<html><head><meta name=\"robots\" content=\"noindex,nofollow\"></head><body>";

my $script = $ENV{SCRIPT_NAME}; $script =~ s/.*\///;
my $givens = $ENV{QUERY_STRING}; $givens .= '.' x (81 - length ($givens));
my @givens = $givens =~ m/(.)/g;
my @choices = map ("123456789", 0..80);

my @subsets = (

# rows
[ 0,  1,  2,  9, 10, 11, 18, 19, 20],
[ 3,  4,  5, 12, 13, 14, 21, 22, 23],
[ 6,  7,  8, 15, 16, 17, 24, 25, 26],
[27, 28, 29, 36, 37, 38, 45, 46, 47],
[30, 31, 32, 39, 40, 41, 48, 49, 50],
[33, 34, 35, 42, 43, 44, 51, 52, 53],
[54, 55, 56, 63, 64, 65, 72, 73, 74],
[57, 58, 59, 66, 67, 68, 75, 76, 77],
[60, 61, 62, 69, 70, 71, 78, 79, 80],

# columns
[ 0,  3,  6, 27, 30, 33, 54, 57, 60],
[ 1,  4,  7, 28, 31, 34, 55, 58, 61],
[ 2,  5,  8, 29, 32, 35, 56, 59, 62],
[ 9, 12, 15, 36, 39, 42, 63, 66, 69],
[10, 13, 16, 37, 40, 43, 64, 67, 70],
[11, 14, 17, 38, 41, 44, 65, 68, 71],
[18, 21, 24, 45, 48, 51, 72, 75, 78],
[19, 22, 25, 46, 49, 52, 73, 76, 79],
[20, 23, 26, 47, 50, 53, 74, 77, 80],

#squares
[ 0,  1,  2,  3,  4,  5,  6,  7,  8],
[ 9, 10, 11, 12, 13, 14, 15, 16, 17],
[18, 19, 20, 21, 22, 23, 24, 25, 26],
[27, 28, 29, 30, 31, 32, 33, 34, 35],
[36, 37, 38, 39, 40, 41, 42, 43, 44],
[45, 46, 47, 48, 49, 50, 51, 52, 53],
[54, 55, 56, 57, 58, 59, 60, 61, 62],
[63, 64, 65, 66, 67, 68, 69, 70, 71],
[72, 73, 74, 75, 76, 77, 78, 79, 80]
);

# remove choices eliminated by the at-most-one rule

for my $subset (@subsets) {
    for my $i (@$subset) {
        next if (my $digit = $givens[$i]) eq '.';
        for my $j (@$subset) {
            $choices[$j] =~ s/$digit//;
        }
    }
}

# identify choices mandated by the at-least-one rule

my @unique;
for my $subset (@subsets) {
    my (@counts, @where);
    for my $i (@$subset) {
        next unless ($givens[$i]) eq '.';
        my $choices = $choices[$i];
        for my $d ($choices =~ m/(.)/g) {
            $counts[$d]++;
            $where[$d]=$i;
        }
    }
    for my $d (1..9) {
        next unless $counts[$d] == 1;
        $unique[$where[$d]] = $d;
    }
}

# display board as table of tables of hyperlinked choices

sub table {
    my $row = "\n<td bgcolor=#77bbff width=170 height=50 align=center>$_[0]" x 3;
    return "\n<table> " . "<tr>$row" x 3 . "</table>";
}

my $board = table(table("X"));

sub choices {
    my ($choice, $i) = @_;
    my $unique = $unique[$i];
    my $result;
    for my $digit ($choice =~ /(.)/g) {
        if ($unique and $digit != $unique) {
            $result .= "\n<font color=gray size=-1>$digit</font>";
        } else {
            $_ = $givens;
            s/(.{$i})./$1$digit/;
            $result .= "\n<a href=$script?$_>$digit</a>";
        }
    }
    return $result;
}

for my $i (0..80) {
    my $digit = $givens[$i];
    my $content = $digit ne '.'
        ? "<font size=8>$digit"
        : choices ($choices[$i], $i);
    $board =~ s/X/$content/;
}

my $blank = "." x 81;

print <<"";
    <style>
        a         {text-decoration: none;}
    </style>
    <center><br>
    $board
    <font color=gray><br><br>
    About Sudokant, the Sudoku Servant
    <br><table width=400><tr><td><font color=gray>
    This server helps you solve <a href=http://www.sudoku.com/>sudoku puzzles</a> 
    by showing choices satisfying two simple rules: <i>at-most-one</i> and <i>at-least-one</i> 
    of every digit must appear in every row, column and square. 
    Choices that violate the <i>at-most-one</i> rule are omitted. 
    Alternatives to digits that must be chosen by the <i>at-least-one</i> rule are grayed.
    <br><br>
    Empty squares are dead ends. Back up the browser 
    to make a new choice. The above rules are insufficent to solve the hardest
    puzzles. You can guess or look for a third rule. This program will help you
    find these most interesting places in the hardest puzzles without becomming a
    mechanical master of the simpler logic.
    <br><br>Each step has a unique url that you can bookmark or send to 
    friends. Use this <a href=$script?$blank>blank page</a> to enter new puzzles.
    Contribute your insights and most diabolical puzzles to the
    <a href=http://c2.com/cgi/wiki?SuDoku>SuDoku</a> page on my wiki.
    </table><br>
    <font color=gray size=-1>
    &copy;2005 Ward Cunningham

